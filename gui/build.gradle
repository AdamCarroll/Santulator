/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */
import org.apache.tools.ant.filters.*

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

mainClassName = "io.github.santulator.gui.main.SantulatorGuiExecutable"

applicationName = "Santulator"

ext.buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')

processResources {
    filesMatching('properties/*.properties') {
        filter ReplaceTokens, tokens: [
                'build.version': project.property("version"),
                'build.timestamp': project.buildTimestamp
        ]
    }
}

jar {
    from(buildDir) {
        include 'build-info.properties'
    }
}

def platform
if (operatingSystem.isWindows()) {
    platform = 'win'
} else if (operatingSystem.isLinux()) {
    platform = 'linux'
} else if (operatingSystem.isMacOsX()) {
    platform = 'mac'
}

configurations {
    javafxCompile
}

dependencies {
    implementation project(':core')
    implementation ('com.gluonhq:ignite-guice:1.0.2') {
        exclude group: 'com.google.inject', module: 'guice'
    }
    implementation 'org.controlsfx:controlsfx:11.1.0'

    // This is required for the conditional logic in the logback setup
    runtimeOnly 'org.codehaus.janino:janino:3.1.3'

    testImplementation project(':test-utils')
    testImplementation 'org.testfx:testfx-junit5:4.0.16-alpha'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'

    testRuntimeOnly 'commons-io:commons-io:2.8.0'
    testRuntimeOnly 'org.testfx:openjfx-monocle:jdk-12.0.1+2'
}

javafx {
    modules = [
        'javafx.controls', 'javafx.fxml'
    ]
    version = '13.0.2'
}

test {
    if (project.hasProperty("skipGuiTests")) {
        exclude 'io/github/santulator/gui/main/GuiTest*'
    }
    if (!project.hasProperty("noHeadless")) {
        jvmArgs "-Dheadless=true"
    }
}

run {
    jvmArgs += [
        '--add-exports', 'javafx.graphics/com.sun.javafx.css=ALL-UNNAMED'
    ]
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    from jar
    exclude "javafx-*.jar"
    into "${buildDir}/libraries"
}

task copyJavafxModules(type: Copy) {
    from configurations.runtimeClasspath
    include "javafx-*${javafx.platform.classifier}.jar"
    into "${buildDir}/javafx-modules"
}
